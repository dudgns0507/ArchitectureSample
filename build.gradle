// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        min_sdk = 21
        target_sdk = 31
        version_code = 1
        version_name = '1.0.0'

        kotlin_version = "1.5.21"
        hilt_version ='2.38.1'
        room_version = '2.3.0'
        compose_version = '1.0.2'
        detekt_version = '1.18.1'
        ktlint_version = '10.2.0'
    }

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.4"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_version"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        toolVersion = "$detekt_version"
        input = files("src/main/java")
        config = files("../detekt-config.yml")
        autoCorrect = true
        reports {
            xml {
                enabled = true
            }
            html {
                enabled = true
            }
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"
    }

    ktlint {
        debug = true
        android = true
        ignoreFailures = true
    }
}